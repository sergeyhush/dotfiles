export DOTFILES="${"${(%):-%N}":A:h:h}"
export ZSH_HOME="$HOME/.zsh"
export ZPLUG_HOME="$HOME/.zsh/zplug"
export FZF_BASE="/usr/local/opt/fzf"

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files first
for file in ${(M)config_files:#*/path.zsh}
do
    source "$file"
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
    source "$file"
done

# autocompletion systems
autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
  compinit
done
compinit -C

for file in ${(M)config_files:#*/completion.zsh}
do
    source "$file"
done

unset config_files

source "$ZPLUG_HOME/init.zsh"
zplug "zplug/zplug", hook-build:'zplug --self-manage'

zplug "clvv/fasd",              as:command, use:fasd
zplug "junegunn/fzf",           as:command
zplug "plugins/docker",         from:oh-my-zsh, if:'[[ $commands[docker] ]]'
zplug "plugins/docker-compose", from:oh-my-zsh, if:'[[ $commands[docker-compose] ]]'
zplug "plugins/z",              from:oh-my-zsh
zplug "plugins/fancy-ctrl-z",   from:oh-my-zsh
zplug "plugins/pyenv",          from:oh-my-zsh
zplug "mafredri/zsh-async",     from:github, defer:0
zplug "djui/alias-tips"
zplug "zsh-users/zsh-autosuggestions"
zplug "zsh-users/zsh-completions"

# Fish-like syntax highlighting
#     *  must be loaded after executing compinit command and sourcing
#        other plugins (taken care of with `defer` option)
zplug "zsh-users/zsh-syntax-highlighting", defer:2
zplug "zdharma/fast-syntax-highlighting"
zplug "sindresorhus/pure", from:github, use:pure.zsh, as:theme
zplug "plugins/aws",       from:oh-my-zsh
zplug "plugins/tmux",      from:oh-my-zsh


# fzf
zplug 'junegunn/fzf', \
    as:command, \
    use:'bin/{fzf,fzf-tmux}', \
    if:"[[ $OSTYPE == linux* || $OSTYPE == darwin* ]]", \
        hook-build:'./install --key-bindings --completion --no-update-rc'
        zplug "junegunn/fzf", from:github, use:"shell/completion.zsh"
        zplug "junegunn/fzf", from:github, use:"shell/key-bindings.zsh"


zplug check || zplug install

# =============================================================================
#                                   Startup
# =============================================================================
if zplug check 'zsh-users/zsh-syntax-highlighting'; then
    typeset -gA ZSH_HIGHLIGHT_STYLES ZSH_HIGHLIGHT_PATTERNS

    ZSH_HIGHLIGHT_STYLES[default]='none'
    ZSH_HIGHLIGHT_STYLES[unknown-token]='fg=red'
    ZSH_HIGHLIGHT_STYLES[reserved-word]='fg=yellow'
    ZSH_HIGHLIGHT_STYLES[alias]='fg=blue'
    ZSH_HIGHLIGHT_STYLES[builtin]='fg=blue'
    ZSH_HIGHLIGHT_STYLES[function]='fg=blue'
    ZSH_HIGHLIGHT_STYLES[command]='fg=blue'
    ZSH_HIGHLIGHT_STYLES[precommand]='none'
    ZSH_HIGHLIGHT_STYLES[commandseparator]='none'
    ZSH_HIGHLIGHT_STYLES[hashed-command]='fg=blue'
    ZSH_HIGHLIGHT_STYLES[path]='none'
    ZSH_HIGHLIGHT_STYLES[path_prefix]='none'
    ZSH_HIGHLIGHT_STYLES[path_approx]='fg=yellow'
    ZSH_HIGHLIGHT_STYLES[globbing]='fg=green'
    ZSH_HIGHLIGHT_STYLES[history-expansion]='fg=green'
    ZSH_HIGHLIGHT_STYLES[single-hyphen-option]='fg=magenta'
    ZSH_HIGHLIGHT_STYLES[double-hyphen-option]='fg=red'
    ZSH_HIGHLIGHT_STYLES[back-quoted-argument]='none'
    ZSH_HIGHLIGHT_STYLES[single-quoted-argument]='fg=yellow'
    ZSH_HIGHLIGHT_STYLES[double-quoted-argument]='fg=yellow'
    ZSH_HIGHLIGHT_STYLES[dollar-double-quoted-argument]='fg=cyan'
    ZSH_HIGHLIGHT_STYLES[back-double-quoted-argument]='fg=cyan'
    ZSH_HIGHLIGHT_STYLES[assign]='none'
fi

if zplug check 'plugins/tmux'; then
    ZSH_TMUX_AUTOSTART=true
    ZSH_TMUX_AUTOSTART_ONCE=true
    if [[ $OSTYPE = (darwin)* ]]; then
        ZSH_TMUX_ITERM2=true
    fi
fi

zplug load


# change the prompt success color
zstyle :prompt:pure:prompt:success color white

fpath=("$HOME/bin" $fpath)

[ -f ~/.localrc ] && source ~/.localrc
