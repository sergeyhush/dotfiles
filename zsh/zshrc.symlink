# Autoload module functions
# () {
#   setopt LOCAL_OPTIONS EXTENDED_GLOB

#   # autoload searches fpath for function locations; add enabled module function paths
#   fpath=(${HOME}/.zsh/functions ${fpath})
#   autoload -U ${HOME}/.zsh/functions/*(:t)
# }

export DOTFILES="${"${(%):-%N}":A:h:h}"
export ZSH_HOME="$HOME/.zsh"

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files first
for file in ${(M)config_files:#*/path.zsh}
do
    source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
    source $file
done

# autocompletion systems
autoload -U compinit
compinit
# autoload -U ~/.zsh/completion/*(:t)
for file in ${(M)config_files:#*/completion.zsh}
do
    source $file
done

unset config_files
